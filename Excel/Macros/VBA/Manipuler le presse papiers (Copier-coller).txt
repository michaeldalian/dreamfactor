VB6
Clipboard.SetText "Hello"
Text1.Text = Clipboard.GetText 'Retourne "Hello"
Clipboard.Clear 'Vide le Presse-papiers
Clipboard.SetData Image1.Picture, vbCFBitmap

Access 95 -------------------------------------------------

NOTE: Dans ACCESS version 2, on peut utiliser l'action OLE_COPY pour
copier un champ OLE dans le Presse-papier. Pour plus d'information voir
la propriété "Action" dans L'aide en ligne.


EXEMPLE :

1. Créez un nouveau module.

2. Tapez les lignes suivantes dans la section déclarations du module :

NOTE: le caractère (_) est utilisé comme caractère de continuation de
ligne.

Declare Function GlobalUnlock Lib "kernel32" (ByVal hMem As Long) _
As Long
Declare Function GlobalLock Lib "kernel32" (ByVal hMem As Long) _
As Long
Declare Function GlobalAlloc Lib "kernel32" (ByVal wFlags As Long,
_
ByVal dwBytes As Long) As Long
Declare Function CloseClipboard Lib "User32" () As Long
Declare Function OpenClipboard Lib "User32" (ByVal hwnd As Long) _
As Long
Declare Function EmptyClipboard Lib "User32" () As Long
Declare Function lstrcpy Lib "kernel32" (ByVal lpString1 As Any, _
ByVal lpString2 As Any) As Long
Declare Function SetClipboardData Lib "User32" (ByVal wFormat _
As Long, ByVal hMem As Long) As Long

Public Const GHND = &H42
Public Const CF_TEXT = 1
Public Const MAXSIZE = 4096

3. Ajoutez la procedure suivante :

Function ClipBoard_SetData(MyString As String)
Dim hGlobalMemory As Long, lpGlobalMemory As Long
Dim hClipMemory As Long, X As Long

' Allocation de mémoire Global.
hGlobalMemory = GlobalAlloc(GHND, Len(MyString) + 1)

' Reserve ce bloc et retourne un pointeur de
' type Far sur ce bloc.
lpGlobalMemory = GlobalLock(hGlobalMemory)

' Copie de la chaine dans ce bloc.
lpGlobalMemory = lstrcpy(lpGlobalMemory, MyString)

' Restitue le bloc de mémoire.
If GlobalUnlock(hGlobalMemory) <> 0 Then
MsgBox "Echec à la restitution de la mémoire. Abandon de la copie."
GoTo OutOfHere2
End If

' Ouvre le Presse-Papiers
If OpenClipboard(0&) = 0 Then
MsgBox "Echec à l'ouverture du Presse-Papiers. Abandon de la copie"
Exit Function
End If

' Vide le Presse_Papiers.
X = EmptyClipboard()

' Copie les données dans le Presse-Papier.
hClipMemory = SetClipboardData(CF_TEXT, hGlobalMemory)

OutOfHere2:

If CloseClipboard() = 0 Then
MsgBox "Echec à la fermeture du Presse-papiers"
End If

End Function

4. Dans la fenêtre d'exécution tapez :
ClipBoard_SetData("AAAA")

5. Dans la fenêtre module, choisissez Edition/Coller et le texte "AAAA"
contenu maintenant dans le Presse-Papiers s'affiche.

